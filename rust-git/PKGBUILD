_realname=rust
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='Rust, a system programming language'
arch=('any')
pkgver=1.13.0.20160913
pkgrel=1
url='http://www.rust-lang.org'
license=('MIT' 'Apache 2.0')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}}")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=('curl' 'python2' "${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-llvm")
install='rust.install'
source=(
  'git+https://github.com/rust-lang/rust'
  '0001-Unstaged.patch'
  '0001-rust-installer-Unstaged.patch'
)

pkgver() {
  cd "${_realname}"
  rel=`grep CFG_RELEASE_NUM= mk/main.mk | sed 's/CFG_RELEASE_NUM=//g'`
  date=`git log -1 --date=short --pretty=format:'%cd' | sed 's/-//g'`
  echo $rel.$date
}

prepare() {
  patch -d "${_realname}" -p1 < 0001-Unstaged.patch
  patch -d "${_realname}/src/rust-installer" -p1 < 0001-rust-installer-Unstaged.patch
}

build() {
  cd "${_realname}"
  ./configure --prefix=${MINGW_PREFIX} --llvm-root=${MINGW_PREFIX}
  make -j4
}

check() {
  cd "${_realname}"
  make check
}

package() {
  cd "${_realname}"
  make DESTDIR=$pkgdir install
  rm $pkgdir/mingw64/bin/libgcc*.dll
  rm $pkgdir/mingw64/bin/libstdc*.dll
}

md5sums=('SKIP'
         '52fa875b35eafd00ddf7193982ed8fc8'
         '6a322fe9012b7797df825818a1b88da8')
